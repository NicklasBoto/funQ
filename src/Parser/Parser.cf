PDef.  Program ::= [FunDec] ;

--- Terms ---
TVar  . Term3 ::= Var                                                    ;
TBit  . Term3 ::= Bit                                                    ;
TGate . Term3 ::= Gate                                                   ;
TTup  . Term3 ::= Tup                                                    ; 
TStar . Term3 ::= "*"                                                    ;
TApp  . Term2 ::= Term2 Term3                                            ; 
TIfEl . Term1 ::= "if" Term2 "then" Term "else" Term                     ; 
TLet  . Term1 ::= "let" "(" LetVar "," [LetVar] ")" "=" Term "in" Term   ;
TLamb . Term1 ::= Lambda FunVar Type "." Term                            ; 
coercions Term 3;

LVar . LetVar ::= Var;
separator nonempty LetVar ",";

Tuple. Tup ::= "(" Term "," [Term] ")" ;
separator nonempty Term ",";

--- Bits ---
BZero. Bit ::= "0";
BOne.  Bit ::= "1";

--- Function declaration  --- 
FDecl. FunDec ::= FunVar Type Function;
terminator FunDec "";

FDef.  Function ::= Var [Arg] "=" Term;

FArg. Arg ::= Var ;
separator Arg " ";

comment "--" ; 

token FunVar (lower (letter | digit | '_' | '\'')* (' ')* ':') ;
 
--- Var ---
token Var (lower (letter | digit | '_' | '\'')*) ;

--- GateIdent ---
token GateIdent ((upper)+) ;

--- Lambda token ---
-- token Lambda char (0xBB) ;
token Lambda '\\' ;

--- Type ---
TypeBit  . Type2  ::= "Bit"            ;
TypeQbit . Type2  ::= "QBit"           ;
TypeVoid . Type2  ::= "T"              ;
TypeDup  . Type2  ::= "!" Type2        ;
TypeTens . Type1  ::= Type2 "><" Type1 ;
TypeFunc . Type1  ::= Type2 "-o" Type1 ;
coercions Type 2;

--- Gates ---
GH    . Gate ::= "H"       ;
GX    . Gate ::= "X"       ;
GY    . Gate ::= "Y"       ;
GZ    . Gate ::= "Z"       ;
GI    . Gate ::= "I"       ;
GS    . Gate ::= "S"       ;
GT    . Gate ::= "T"       ;
GCNOT . Gate ::= "CNOT"    ;
GTOF  . Gate ::= "TOFFOLI" ;
GSWP  . Gate ::= "SWAP"    ;
GFRDK . Gate ::= "FREDKIN" ;
GQFT  . Gate ::= "QFT"     ;
GQFTI . Gate ::= "QFTI"    ;
GCR  . Gate ::= "CR"       ;
GCRD . Gate ::= "CRD"      ;
GCR2  . Gate ::= "CR2"     ;
GCR2D . Gate ::= "CR2D"    ;
GCR3  . Gate ::= "CR3"     ;
GCR3D . Gate ::= "CR3D"    ;
GCR4  . Gate ::= "CR4"     ;
GCR4D . Gate ::= "CR4D"    ;
GCR5  . Gate ::= "CR5"     ;
GCR5D . Gate ::= "CR5D"    ;
GCR8  . Gate ::= "CR8"     ;
GCR8D . Gate ::= "CR8D"    ;
GGate . Gate ::= GateIdent ; 